buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.1'
    }
}

allprojects {
    group = 'io.infrastructor'
    version '0.3.2-SNAPSHOT'

    repositories {
        mavenLocal()
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'maven-publish'

    sourceCompatibility = '1.8'
    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    sourceSets {
        main {
            groovy {
                srcDirs = ['src/main/java']
            }
        }
        test {
            groovy {
                srcDirs = ['src/test/java']
            }
        }
    }

    test {
        systemProperty "LOG_LEVEL", "2"
        systemProperty "ENABLE_DYNAMIC_LOGS", "false"
        testLogging.showStandardStreams = true
    }

    dependencies {
        implementation 'ch.qos.logback:logback-classic:1.2.9'
        implementation 'com.jcraft:jsch:0.1.55'
        implementation 'javax.validation:validation-api:2.0.1.Final'
        implementation 'org.apache.bval:bval-jsr:1.1.2'
        implementation 'org.codehaus.groovy:groovy-all:3.0.9'
        implementation 'org.apache.ivy:ivy:2.5.0'
        implementation 'org.apache.httpcomponents:httpclient:4.5.13'
        implementation 'javax.xml.bind:jaxb-api:2.3.1'
        implementation 'org.apache.ant:ant:1.10.11'
        implementation 'org.fusesource.jansi:jansi:2.4.0'
        testImplementation "junit:junit:4.12"
    }

    if (project.hasProperty('bintrayUser') && project.hasProperty('bintrayPass')) {
        groovydoc {
            source = sourceSets.main.allSource
            classpath = configurations.compileClasspath
        }

        task sourceJar(type: Jar) {
            from sourceSets.main.allSource
            classifier = 'sources'
        }

        task groovydocJar(type: Jar) {
            from groovydoc
            classifier = 'groovydoc'
        }

        publishing {
            publications {
                allComponents(MavenPublication) {
                    from components.java
                    artifact sourceJar
                    artifact groovydocJar
                }
            }
        }

        bintray {
            user = property('bintrayUser')
            key = property('bintrayPass')
            publications = ['allComponents']
            pkg {
                repo = 'public'
                name = 'infrastructor'
                userOrg = 'infrastructor'
            }
        }
    }
}
